setopt prompt_subst

function prompt() {
    if [[ "root" == `whoami` ]]; then
        local bracketColor='red'
    else
        local bracketColor='green'
    fi

    function _vcs() {
        local bracketColor=$1
        if [[ -n `git branch 2>/dev/null` ]]; then
            local branch="$(git branch | sed -n '/^\*/s/^\* //p;')"
            local gitStat="$(git diff --shortstat | sed -r 's/[^0-9]+/ /g')"
            local gitStatChanged="$(echo $gitStat | cut -d' ' -f2)"
            local gitStatPlus="$(echo $gitStat | cut -d' ' -f3)"
            local gitStatMinus="$(echo $gitStat | cut -d' ' -f4)"
            local untracked="$(git status --porcelain | grep "^??" | wc -l)"

            echo -n "%F{$bracketColor}[%F{red}Â± %F{green}$branch%F{reset}"
            if [[ "$gitStatPlus $gitStatMinus $gitStatChanged" != "  " ]]; then
                echo -n " (%F{green}+$gitStatPlus%F{reset}/%F{red}-$gitStatMinus%F{reset}, %F{yellow}$gitStatChanged%F{reset})"
            fi
            if [[ "$untracked" != "0" ]]; then
                echo -n " %F{magenta}$untracked"
            fi
            echo -n "%F{$bracketColor}]"
        fi
        #TODO: hg, svn, and so on
    }

    function _hidden() {
        local bracketColor=$1

        local running="$(jobs -r | wc -l)"
        local stopped="$(jobs -s | wc -l)"
        local detach="$(tmux ls 2>/dev/null | grep -v "(attached)$" | wc -l)"
        local users="$(who | wc -l)"

        if [[ "${running}${stopped}${detach}${users}" != "0001" ]]; then
            echo -n "%F{$bracketColor}[%F{yellow}"
            if [[ $running != "0" ]]; then
                echo -n "${running}&"
            fi
            if [[ $stopped != "0" ]]; then
                echo -n "${stopped}z"
            fi
            if [[ $detach != "0" ]]; then
                echo -n "${detach}d"
            fi
            if [[ $users != "1" ]]; then
                echo -n "${users}u"
            fi
            echo -n "%F{$bracketColor}]"
        fi
    }

    local basePrompt="%F{$bracketColor}[%F{red}%n%F{reset}@%F{magenta}%m%F{reset}:%F{blue}%~%F{$bracketColor}]%F{reset}%# "
    case "$1" in
        "heavy")
            PROMPT=$basePrompt
            RPROMPT="\$(_hidden $bracketColor)\$(_vcs $bracketColor)%F{$bracketColor}[%F{cyan}%T%F{$bracketColor}]%F{reset}"
            ;;
        "normal")
            PROMPT=$basePrompt
            RPROMPT=''
            ;;
        "light")
            PROMPT='%# '
            RPROMPT=''
            ;;
    esac

    function _prompt() {
        compadd light normal heavy
    }

    compdef _prompt prompt
}

prompt heavy

